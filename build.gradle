plugins {
    id 'fabric-loom' version '1.3-SNAPSHOT'
    id "com.modrinth.minotaur" version "2.+"

    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.0'
    id 'org.jetbrains.kotlin.plugin.allopen' version '1.9.0'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.0'
}

version = project.evo_plus_version
group = project.maven_group

repositories {
    mavenCentral()
    maven { url 'https://gitlab.diamondworld.pro/api/v4/projects/104/packages/maven' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    // Fabric
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    include(modImplementation(fabricApi.module("fabric-message-api-v1", project.fabric_version)))
//    include(modImplementation(fabricApi.module("fabric-command-api-v2", project.fabric_version)))

    //Kotlin
    modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

    //Other dependencies
    include(implementation("com.github.ben-manes.caffeine:caffeine:${project.caffeine_version}"))
    include(implementation("pro.diamondworld.evoplusprotocol:evoplusprotocol:${project.evo_plus_protocol_version}"))
    include(implementation("com.github.asyncdargen:rest-client:${project.rest_client_version}"))
    include(implementation("com.github.asyncdargen.crowbar:crowbar-core:${project.crowbar_version}"))

    compileOnly(annotationProcessor('org.projectlombok:lombok:1.18.24'))
}

runClient {
    jvmArgs '-Devo-plus.dev=true'
    args "--username=${System.getenv('TEST_NAME')}"
}

modrinth {
    token = System.getenv('MODRINTH_TOKEN')
    projectId = 'evoplus'
    versionNumber = project.version
    versionName = project.version
    changelog = new File('changelog.txt').text.split('\n').collect { "- $it" }.join('\n')
    versionType = "release"
    uploadFile = remapJar
    gameVersions = [project.minecraft_version]
    loaders = ['fabric']
    dependencies { project.mod_dependencies.split(',').collect { required.version(it) }}
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(project.java_version.toInteger())
}

kotlin {
    jvmToolchain(project.java_version.toInteger())
}

allOpen {
    annotation('ru.dargen.evoplus.util.kotlin.KotlinOpens')
}

jar {
    from('LICENSE')
}

processResources {
    inputs.property 'version', version
    filteringCharset 'UTF-8'

    filesMatching('fabric.mod.json') {
        expand project.properties
        expand 'version': version
    }
}

tasks.withType(JavaCompile)*.options*.encoding = 'UTF-8'